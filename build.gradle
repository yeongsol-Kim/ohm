buildscript {
    ext {
        springBootVersion = '2.7.5'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}




//setting.gradle에 include 된 프로젝트를 전부 관리한다.
//rootproject까지 적용시키고 싶은 경오 allprojects로 등록하면 된다.
subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'org.example'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        //현재 api-server 모듈에도 의존되어 있다. 테스트 후 db관련 라이브러리를 어디다 배치할지 생각해보자.
        implementation 'mysql:mysql-connector-java'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        runtimeOnly 'com.h2database:h2'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    test {
        useJUnitPlatform()
    }
}

project(':ohm-core') {
    // 공통 코드

    bootJar { enabled = false } // core 은 bootJar 로 패키징 할 필요 없음
    jar { enabled = true }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}

project(':ohm-api-server') {
    bootJar { enabled = true }
    jar { enabled = false }

    //컴파일시 ohm-core에 로딩된다.
    dependencies {
        implementation project(':ohm-core')
    }
}

project(':ohm-batch-server') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':ohm-core')
    }
}